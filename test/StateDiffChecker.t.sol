// SPDX-License-Identifier: MIT
pragma solidity ^0.8.15;

import {Test} from "forge-std/Test.sol";
import {VmSafe} from "forge-std/Vm.sol";
import {stdJson} from "forge-std/StdJson.sol";
import {LibStateDiffChecker as Checker} from "script/LibStateDiffChecker.sol";

/// @dev A simple contract to which makes some state modifications for the purpose of testing state diff checking.
contract Target {
    uint256 public x;
    address public y;
    mapping(uint256 => bytes32) public z;

    Callee public callee;
    DelegateCallee public delegateCallee;

    constructor() {
        x = 1;
        callee = new Callee();
        delegateCallee = new DelegateCallee();
    }

    function set() external {
        x = 0;
        y = address(0xabba);
        z[1] = hex"acdc";

        callee.set1();
        (bool success,) =
            address(callee).delegatecall(abi.encodeWithSelector(callee.set2.selector, address(delegateCallee)));
        require(success, "Target: Delegatecall failed");
    }
}

contract Callee {
    function set1() external {
        assembly {
            sstore(0x1111, 1)
        }
    }

    function set2(DelegateCallee delegateCallee) external {
        (bool success,) = address(delegateCallee).delegatecall("");
        require(success, "Callee: Delegatecall failed");
    }
}

contract DelegateCallee {
    fallback() external {
        assembly {
            sstore(0x2222, 1)
        }
    }
}

/// @dev Tests for LibStateDiffChecker
contract StateDiffChecker_Test is Test {
    Target public target;

    error StateDiffMismatch(string field, bytes32 expected, bytes32 actual);

    function setUp() public {
        target = new Target();
    }

    /// @dev Test that the sample testDiff.json file is read and parsed correctly
    function test_parseDiffSpecs() public {
        string memory _path = "test/testDiff.json";
        string memory json = vm.readFile(_path);
        Checker.StateDiffSpec memory parsedSpec = Checker.parseDiffSpecs(json);

        Checker.StateDiffSpec memory expectedSpec =
            Checker.StateDiffSpec({chainId: 31337, storageSpecs: new Checker.StorageDiffSpec[](5)});

        expectedSpec.storageSpecs[0] = Checker.StorageDiffSpec({
            account: 0x5615dEB798BB3E4dFa0139dFa1b3D433Cc23b72f,
            slot: bytes32(0),
            previousValue: bytes32(uint256(1)),
            newValue: bytes32(0)
        });
        expectedSpec.storageSpecs[1] = Checker.StorageDiffSpec({
            account: 0x5615dEB798BB3E4dFa0139dFa1b3D433Cc23b72f,
            slot: bytes32(uint256(1)),
            previousValue: bytes32(0),
            newValue: bytes32(uint256((0xabba)))
        });
        expectedSpec.storageSpecs[2] = Checker.StorageDiffSpec({
            account: 0x5615dEB798BB3E4dFa0139dFa1b3D433Cc23b72f,
            slot: keccak256(abi.encodePacked(uint256(1), uint256(2))),
            previousValue: bytes32(0),
            newValue: bytes32(hex"acdc")
        });
        expectedSpec.storageSpecs[3] = Checker.StorageDiffSpec({
            account: 0x104fBc016F4bb334D775a19E8A6510109AC63E00,
            slot: bytes32(uint256(0x1111)),
            previousValue: bytes32(0),
            newValue: bytes32(uint256(1))
        });
        expectedSpec.storageSpecs[4] = Checker.StorageDiffSpec({
            account: 0x5615dEB798BB3E4dFa0139dFa1b3D433Cc23b72f,
            slot: bytes32(uint256(0x2222)),
            previousValue: bytes32(0),
            newValue: bytes32(uint256(1))
        });

        for (uint256 i = 0; i < expectedSpec.storageSpecs.length; i++) {
            require(
                keccak256(abi.encode(parsedSpec.storageSpecs[i])) == keccak256(abi.encode(expectedSpec.storageSpecs[i])),
                "StateDiffCheckerTest: StorageDiffSpec parsing failed"
            );
        }
    }

    /// @dev Utility function which sets up the subsequent tests by executing Target.set(), parsing the diff spec
    ///      from testDiff.json, and returning the resulting state diff structs.
    function _executeAndGetDiffs()
        internal
        returns (Checker.StateDiffSpec memory expectedDiff_, Checker.StateDiffSpec memory actualDiff_)
    {
        vm.startStateDiffRecording();
        target.set();
        VmSafe.AccountAccess[] memory accountAccesses = vm.stopAndReturnStateDiff();

        string memory _path = "test/testDiff.json";
        string memory json = vm.readFile(_path);
        expectedDiff_ = Checker.parseDiffSpecs(json);

        actualDiff_ = Checker.extractDiffSpecFromAccountAccesses(accountAccesses);
    }

    /// @dev Test that the state diff generated by running Target.set() matches the expected state diff
    ///      as defined in testDiff.json
    function test_checkStateDiff_succeeds() public {
        (Checker.StateDiffSpec memory expectedDiff, Checker.StateDiffSpec memory actualDiff) = _executeAndGetDiffs();
        Checker.checkStateDiff(expectedDiff, actualDiff);
    }

    /// @dev Test that the correct error is thrown when the chain ID does not match.
    function test_checkStateDiff_chainIdMismatch_reverts() public {
        (Checker.StateDiffSpec memory expectedDiff, Checker.StateDiffSpec memory actualDiff) = _executeAndGetDiffs();
        actualDiff.chainId = 31338;
        vm.expectRevert(abi.encodeWithSelector(StateDiffMismatch.selector, "chainId", 31337, 31338));
        Checker.checkStateDiff(expectedDiff, actualDiff);
    }

    /// @dev Test that the correct error is thrown when the number of storage modifications does not match.
    function test_checkStateDiff_lengthMismatch_reverts() public {
        (Checker.StateDiffSpec memory expectedDiff,) = _executeAndGetDiffs();
        Checker.StateDiffSpec memory shorterDiff = Checker.StateDiffSpec({
            chainId: 31337,
            storageSpecs: new Checker.StorageDiffSpec[](expectedDiff.storageSpecs.length - 1)
        });
        shorterDiff.storageSpecs[0] = expectedDiff.storageSpecs[0];
        vm.expectRevert(
            abi.encodeWithSelector(
                StateDiffMismatch.selector,
                "storageSpecs.length",
                expectedDiff.storageSpecs.length,
                shorterDiff.storageSpecs.length
            )
        );
        Checker.checkStateDiff(expectedDiff, shorterDiff);
    }

    /// @dev A utility function to copy a StateDiffSpec struct so that we can modify it without affecting the original.
    function copyDiff(Checker.StateDiffSpec memory diff) internal pure returns (Checker.StateDiffSpec memory) {
        Checker.StateDiffSpec memory copy;
        copy.chainId = diff.chainId;
        copy.storageSpecs = new Checker.StorageDiffSpec[](diff.storageSpecs.length);
        for (uint256 i = 0; i < diff.storageSpecs.length; i++) {
            copy.storageSpecs[i].account = diff.storageSpecs[i].account;
            copy.storageSpecs[i].slot = diff.storageSpecs[i].slot;
            copy.storageSpecs[i].newValue = diff.storageSpecs[i].newValue;
            copy.storageSpecs[i].previousValue = diff.storageSpecs[i].previousValue;
        }

        // Ensure that all values have been copied over
        require(
            keccak256(abi.encode(copy)) == keccak256(abi.encode(diff)),
            "StateDiffCheckerTest: StorageDiffSpec copying failed"
        );
        return copy;
    }

    /// @dev Test that the correct error is thrown when a storage spec account does not match.
    function test_checkStateDiff_storageSpecAccountMismatch_reverts() public {
        (Checker.StateDiffSpec memory expectedDiff,) = _executeAndGetDiffs();
        Checker.StateDiffSpec memory brokenDiff = copyDiff(expectedDiff);

        brokenDiff.storageSpecs[0].account = address(0);
        vm.expectRevert(
            abi.encodeWithSelector(
                StateDiffMismatch.selector,
                string.concat("storageSpecs[0].account"),
                expectedDiff.storageSpecs[0].account,
                address(0)
            )
        );
        Checker.checkStateDiff(expectedDiff, brokenDiff);
    }

    /// @dev Test that the correct error is thrown when a storage spec slot does not match.
    function test_checkStateDiff_storageSpecSlotMismatch_reverts() public {
        (Checker.StateDiffSpec memory expectedDiff,) = _executeAndGetDiffs();
        Checker.StateDiffSpec memory brokenDiff = copyDiff(expectedDiff);

        // break the slot field
        brokenDiff.storageSpecs[0].slot = bytes32(hex"deadbeef");
        vm.expectRevert(
            abi.encodeWithSelector(
                StateDiffMismatch.selector,
                string.concat("storageSpecs[0].slot"),
                expectedDiff.storageSpecs[0].slot,
                bytes32(hex"deadbeef")
            )
        );
        Checker.checkStateDiff(expectedDiff, brokenDiff);
    }

    /// @dev Test that the correct error is thrown when a storage spec newValue does not match.
    function test_checkStateDiff_storageSpecNewValueMismatch_reverts() public {
        (Checker.StateDiffSpec memory expectedDiff,) = _executeAndGetDiffs();
        Checker.StateDiffSpec memory brokenDiff = copyDiff(expectedDiff);

        // break the newValue field
        brokenDiff.storageSpecs[0].newValue = bytes32(hex"deadbeef");
        vm.expectRevert(
            abi.encodeWithSelector(
                StateDiffMismatch.selector,
                "storageSpecs[0].newValue",
                expectedDiff.storageSpecs[0].newValue,
                bytes32(hex"deadbeef")
            )
        );
        Checker.checkStateDiff(expectedDiff, brokenDiff);
    }

    /// @dev Test that the correct error is thrown when a storage spec previousValue does not match.
    function test_checkStateDiff_storageSpecPreviousValueMismatch_reverts() public {
        (Checker.StateDiffSpec memory expectedDiff,) = _executeAndGetDiffs();
        Checker.StateDiffSpec memory brokenDiff = copyDiff(expectedDiff);

        // break the previousValue field
        brokenDiff.storageSpecs[0].slot = expectedDiff.storageSpecs[0].slot;
        brokenDiff.storageSpecs[0].previousValue = bytes32(hex"deadbeef");
        vm.expectRevert(
            abi.encodeWithSelector(
                StateDiffMismatch.selector,
                "storageSpecs[0].previousValue",
                expectedDiff.storageSpecs[0].previousValue,
                bytes32(hex"deadbeef")
            )
        );
        Checker.checkStateDiff(expectedDiff, brokenDiff);
    }
}
