// SPDX-License-Identifier: MIT
pragma solidity ^0.8.15;

import {Test} from "forge-std/Test.sol";
import {VmSafe} from "forge-std/Vm.sol";
import {stdJson} from "forge-std/StdJson.sol";
import {console2 as console} from "forge-std/console2.sol";
import {LibStateDiffChecker} from "script/LibStateDiffChecker.sol";
import {LibStateDiff} from "@eth-optimism-bedrock/scripts/libraries/LibStateDiff.sol";

contract Target {
    uint256 public x;
    address public y;
    mapping(uint256 => bytes32) public z;

    constructor() {
        x = 1;
    }

    function set() public {
        x = 0;
        y = address(0xabba);
        z[1] = hex"acdc";
    }
}

contract StateDiffChecker_Test is Test {
    Target public target;

    function setUp() public {
        target = new Target();
    }

    /// @dev Test that the sample testDiff.json is read and parsed correctly
    function test_parseDiffSpecs() public {
        string memory _path = "test/testDiff.json";
        string memory json = vm.readFile(_path);
        LibStateDiffChecker.AccountAccessSpec[] memory specs = LibStateDiffChecker.parseDiffSpecs(json);

        assertEq(specs.length, 1);
        assertEq(specs[0].account, stdJson.readAddress(json, "$.[0].account"));
        assertEq(specs[0].storageAccesses.length, 3);
        assertEq(specs[0].storageAccesses[0].newValue, bytes32(0));
        assertEq(specs[0].storageAccesses[1].newValue, bytes32(uint256(uint160(address(0xabba)))));
        assertEq(specs[0].storageAccesses[2].newValue, hex"acdc");
    }

    /// @dev Test that the state diff generated by running Target.set() matches the expected state diff
    ///      as defined in testDiff.json
    function test_checkStateDiff() public {
        vm.startStateDiffRecording();
        target.set();
        VmSafe.AccountAccess[] memory accountAccesses = vm.stopAndReturnStateDiff();
        console.log(LibStateDiff.encodeAccountAccesses(accountAccesses));
        string memory _path = "test/testDiff.json";
        string memory json = vm.readFile(_path);
        LibStateDiffChecker.AccountAccessSpec[] memory specs = LibStateDiffChecker.parseDiffSpecs(json);

        // string memory json = stdJson.readFile(_path);
        LibStateDiffChecker.checkStateDiffs(specs, accountAccesses);
    }
}
